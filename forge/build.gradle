apply plugin: "dev.architectury.loom"

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    forge {
        mixinConfig "protoweaver.mixins.json"
    }
    silentMojangMappingsLicense()
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    shadowCommon(implementation(project(path: ":loader-common", configuration: "default")))
    shadowCommon(project(path: ":loader-common", configuration: "transformProductionForge")) { transitive false }

    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    forge "net.minecraftforge:forge:${project.forge_version}"

    // Libs
    include forgeRuntimeLibrary("com.esotericsoftware:kryo5:${project.kyro_version}")
    include forgeRuntimeLibrary("org.bouncycastle:bcpkix-jdk18on:${project.bouncy_version}")
    include forgeRuntimeLibrary("io.netty:netty-codec-http:${project.netty_version}")
    include forgeRuntimeLibrary("io.netty:netty-codec-http2:${project.netty_version}")

    // Mod Compat
    modImplementation "maven.modrinth:proxy-compatible-forge:${project.proxy_forge_version}"

    //Dev
    modLocalRuntime "maven.modrinth:easy-eula:1.1.0-forge"
}

jar {
    dependsOn(':loader-common:transformProductionForge')
    from {
        exclude "architectury.common.marker"
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.shadowCommon.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

remapJar {
    archiveBaseName.set("protoweaver")
    archiveClassifier.set(project.name)
}

jar.finalizedBy(remapJar)