plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "io.freefair.lombok" version "8.6"
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.5-SNAPSHOT" apply false
    id "maven-publish"
}

allprojects {
    apply plugin: "java-library"
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "io.freefair.lombok"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    version = project.project_version
    group = project.maven_group

    repositories {
        mavenCentral()
        maven { url "https://maven.minecraftforge.net" }                    // Forge
        maven { url "https://repo.papermc.io/repository/maven-public/" }    // Paper + Velocity
        maven { url "https://api.modrinth.com/maven" }                      // Modrinth
    }
    
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = project.javaVersion as Integer
    }
}

subprojects {

    dependencies {
        compileOnly "org.projectlombok:lombok:${project.lombok_version}"
	    annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
    }

    shadowJar {
        archiveBaseName.set("protoweaver")
        archiveClassifier.set(project.name)
        configurations = [project.configurations.shadow]

        dependencies {
            mergeServiceFiles()
        }
    }

    jar {
        archiveBaseName.set("protoweaver")
        archiveClassifier.set(project.name)
    }

    task jarSources(type:Jar){
        archiveBaseName.set("protoweaver")
        archiveVersion.set(project.project_version)
        archiveClassifier.set(project.name + "-sources")
        exclude {
            sourceSets.main.allSource.contains it.file
        }
        from delombok
    }

    jar.finalizedBy(jarSources)

    publishing {
        repositories {
            maven {
                name "MrNavaStar"
                url "https://maven.mrnavastar.rocks/snapshots"
                credentials {
                    username = System.getenv("MAVEN_NAME")
                    password = System.getenv("MAVEN_SECRET")

                }
            }
        }
        publications {
            maven(MavenPublication) {
                groupId = "me.mrnavastar"
                artifactId = "protoweaver"
                version = "debug-build"
                from components.java
                //artifact tasks.jarSources
            }
        }
    }
}