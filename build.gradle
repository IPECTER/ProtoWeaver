plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "io.freefair.lombok" version "8.4"
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.5-SNAPSHOT" apply false
}

allprojects {
    apply plugin: "java-library"
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "io.freefair.lombok"
    apply plugin: "architectury-plugin"

    architectury {
        compileOnly()
    }

    repositories {
        mavenCentral()
        maven { url "https://maven.minecraftforge.net" }                      // Forge
        maven { url "https://repo.papermc.io/repository/maven-public/" }	    // Paper + Velocity
        maven { url "https://api.modrinth.com/maven" }                          // Modrinth
    }
}

subprojects {
    version = "debug"
    group = project.maven_group

    configurations {
        common
        shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
        compileClasspath.extendsFrom common
        runtimeClasspath.extendsFrom common
        developmentForge.extendsFrom common
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:${project.lombok_version}"
	    annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
    }

    /*sourcesJar {
        exclude {
            sourceSets.main.allSource.contains it.file
        }
        from delombok
    }*/

    shadowJar {
        archiveClassifier.set("shadow")
        configurations = [project.configurations.shadowCommon]

        dependencies {
            mergeServiceFiles()
        }
    }

    java {
        javadoc.options.addStringOption('Xdoclint:none', '-quiet')
        withSourcesJar()
        withJavadocJar()
    }
}




